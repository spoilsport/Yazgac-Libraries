**************************************************
* Level 1 SPICE Models for the 4007 CMOS chip
**************************************************
* These Level 1 models were extracted from measured results. The
* model attempts to account for the package parasitics. The simulated and
* typical measured device capacitances are as follows:
*
*    NMOS: Cgs = 18pF   Cds = 14pF
*    PMOS: Cgs = 17pF   Cds = 26pF
*
* Gate-to-drain capacitances were not extracted but were adjusted to
* to help fit measured results.
*$
**************************************************
* Macro for 4007 IC:
**************************************************
*
* Pinout:
*
*             S4/psub G1,4 D5   S5   G5,2 S2   D2      * 1,2,3 - PMOS
*               _||___||___||___||___||___||___||_     * 4,5,6 - NMOS
*               | 7   6    5    4    3    2    1 |     * All PMOS susbstrates 
*               |                                |       connected to pin 14
*               |                              * |     * All NMOS susbstrates
*               |                                |       connected to pin 7
*               | 8   9    10   11   12   13   14|
*               _  ___  ___  ___  ___  ___  ___  _
*                ||   ||   ||   ||   ||   ||   ||
*                D4   S6   G3,6 S3   D3,6 D1   S1/nsub
*
* General Form of subcircuit call:
*  X1 n1 n2 ... n14 CMOS4007
*
*$
.SUBCKT CD4007 1 2 3 4 5 6 7 8 9 10 11 12 13 14
* MOSFET DR GT SRC SUBS MODEL L    W
M1       13 6  14  14   CMOSP L=5u W=100u
M2       1  3  2   14   CMOSP L=5u W=100u
M3       12 10 11  14   CMOSP L=5u W=100u
M4       8  6  7   7    CMOSN L=5u W=20u
M5       5  3  4   7    CMOSN L=5u W=20u
M6       12 10 9   7    CMOSN L=5u W=20u

.MODEL CMOSP    PMOS    
+ Level=1 Gamma=0 
+ Tox=1200n Phi=.6 Rs=0 Kp=55u Vto=1.5
+ Lambda=0.04 Rd=0 Cbd=4.0p Cbs=4.0p
+ Pb=.8 Cgso=0.2p Cgdo=0.2p Is=16.64p N=1
*$
.MODEL CMOSN    NMOS
.model NMOS NMOS Level=1 Gamma=0 
+ Tox=1200n Phi=.6 Rs=0 Kp=111u Vto=2.0
+ Lambda=0.01 Rd=0 Cbd=2.0p Cbs=2.0p
+ Pb=.8 Cgso=0.1p Cgdo=0.1p Is=16.64p N=1

.ENDS CD4007
*

*$